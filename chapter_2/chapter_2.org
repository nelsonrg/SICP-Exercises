#+SETUPFILE: ~/org/org-html-themes/org/theme-bigblow.setup
#+property: header-args :racket :lang sicp
#+property: header-args :exports both
#+OPTIONS: num:nil
* Exercises
** 2.1

#+begin_src racket :results output
(define (get-sign x)
  (if (>= x 0)
      1
      -1))

(define (make-rat n d)
  (let ((g (gcd n d))
        (sign (get-sign (* n d))))
    (cons (/ (* sign (abs n)) g)
          (/ (abs d) g))))

(define (numer x)
  (car x))

(define (denom x)
  (cdr x))

(define (print-rat x)
  (newline)
  (display (numer x))
  (display "/")
  (display (denom x)))

(print-rat (make-rat 4 6))
(print-rat (make-rat -4 -6))
(print-rat (make-rat -4 6))
(print-rat (make-rat 4 -6))
#+end_src

#+RESULTS:
: 
: 2/3
: 2/3
: -2/3
: -2/3

** 2.2

#+begin_src racket :results output
(define (make-point x y)
  (cons x y))

(define (x-point point)
  (car point))

(define (y-point point)
  (cdr point))

(define (make-segment start-point end-point)
  (cons start-point end-point))

(define (start-segment segment)
  (car segment))

(define (end-segment segment)
  (cdr segment))

(define (mid-point segment)
  (let ((point-list (list (start-segment segment) (end-segment segment))))
    (make-point (average (map x-point point-list))
                (average (map y-point point-list)))))

(define (print-point p)
  (newline)
  (display "(")
  (display (x-point p))
  (display ",")
  (display (y-point p))
  (display ")"))

(define (print-segment s)
  (print-point (start-segment s))
  (print-point (end-segment s)))

(define (sum x)
  (apply + x))

(define (length x)
  (if (null? x)
      0
      (+ 1 (length (cdr x)))))

(define (average x)
  (/ (sum x)
     (length x)))

(define origin (make-point 0 0))
(define p1 (make-point 1 1))
(define segment1 (make-segment origin p1))
(print-segment segment1)
(print-point (mid-point segment1))
#+end_src

#+RESULTS:
: 
: (0,0)
: (1,1)
: (1/2,1/2)

** 2.4

#+begin_src racket :results output
#lang sicp

(define (cons x y)
  (lambda (m) (m x y)))

(define (car z)
  (z (lambda (p q) p)))

(define (cdr z)
  (z (lambda (p q) q)))

(car (cons 1 2))
(cdr (cons 1 2))
#+end_src

#+RESULTS:
: 1
: 2

** 2.5

#+begin_src racket :results output
(define (make-pair a b)
  (* (expt 2 a) (expt 3 b)))

(define (cons x y)
  (make-pair x y))

(define (car z)
  (define (car-iter x counter)
    (if (= 0 (remainder x 2))
        (car-iter (/ x 2) (+ counter 1))
        counter))
  (car-iter z 0))

(define (cdr z)
  (define (cdr-iter x counter)
    (if (= 0 (remainder x 3))
        (cdr-iter (/ x 3) (+ counter 1))
        counter))
  (cdr-iter z 0))

(car (make-pair 2 3))
(cdr (make-pair 2 3))
#+end_src

#+RESULTS:
: 2
: 3

** 2.6

This one took me a little bit to wrap my head around. The key idea is
that each Church numeral is a function that takes two parameters: $f$
a function to apply and $x$ a value to apply the function on. The
order of the numeral is the number of times the function is applied to
the input argument. Below I show examples of adding one to zero.

#+begin_src racket :results output
(define zero (lambda (f) (lambda (x) x)))

(define (add-1 n)
  (lambda (f) (lambda (x) (f ((n f) x)))))

(define one (lambda (f)
              (lambda (x)
                (f x))))

(define two (lambda (f)
              (lambda (x)
                (f (f x)))))

(define (add a b)
  (lambda (f)
    (lambda (x)
      ((a f)
       ((b f) x)))))

((zero (lambda (x) (+ x 1))) 0)
((one (lambda (x) (+ x 1))) 0)
((two (lambda (x) (+ x 1))) 0)
(((add one two) (lambda (x) (+ x 1))) 0)
#+end_src

#+RESULTS:
: 0
: 1
: 2
: 3

** 2.7

#+begin_src racket :results output
(define (make-interval a b)
  (cons a b))

(define (lower-bound interval)
  (car interval))

(define (upper-bound interval)
  (cdr interval))

(define interval (make-interval 5.0 10.0))
(lower-bound interval)
(upper-bound interval)
#+end_src

#+RESULTS:
: 5.0
: 10.0

** 2.8

We should just be able to use the /add-interval/ procedure and replace
the addition function with a subtraction.

#+begin_src racket :results output
(define (subtract-interval x y)
  (make-interval (- (lower-bound x) (lower-bound y))
                 (- (upper-bound x) (upper-bound y))))

(define (make-interval a b)
  (cons a b))

(define (lower-bound interval)
  (car interval))

(define (upper-bound interval)
  (cdr interval))

(define interval1 (make-interval 5.0 10.0))
(define interval2 (make-interval 0.0 5.0))
(subtract-interval interval1 interval2)
#+end_src

#+RESULTS:
: (5.0 . 5.0)

** 2.9

First let's define notation for the intervals. Let the upper bound of
an interval be denoted as $b$ and the lower bound as $a$.Then the
width of an interval $n$ is

$W_n = b_n - a_n$.

For an interval formed by summing the intervals $m$ and $n$, the upper bound is

$b_{m + n} = b_m + b_n$

and the lower bound is

$a_{m + n} = a_m + a_n$.

Therefore the width of the addition of the intervals is given by

\begin{align*}
  W_{m + n} &= b_{m + n} - a_{m + n} \\
            &= b_m + b_n - a_m - a_n \\
            &= (b_m - a_m) + (b_n - a_n) \\
            &= W_m + W_n
\end{align*}

so the width of the addition of two intervals is equal to the addition
of their widths.

#+begin_src racket :results output
(define (make-interval a b)
  (cons a b))

(define (lower-bound interval)
  (car interval))

(define (upper-bound interval)
  (cdr interval))

(define (add-interval x y)
  (make-interval (+ (lower-bound x) (lower-bound y))
                 (+ (upper-bound x) (upper-bound y))))

(define (mul-interval x y)
  (let ((p1 (* (lower-bound x) (lower-bound y)))
        (p2 (* (lower-bound x) (upper-bound y)))
        (p3 (* (upper-bound x) (lower-bound y)))
        (p4 (* (upper-bound x) (upper-bound y))))
    (make-interval (min p1 p2 p3 p4)
                   (max p1 p2 p3 p4))))

(define (width interval)
  (/ (- (upper-bound interval)
        (lower-bound interval))
     2))

(define interval1 (make-interval 5.0 10.0))
(define interval2 (make-interval 0.0 5.0))

(display "Adding intervals of same width (5.0)")
(width (add-interval interval1 interval1))
(width (add-interval interval2 interval2))
(width (add-interval interval1 interval2))

(display "Multiplying intervals of same width (5.0)")
(width (mul-interval interval1 interval1))
(width (mul-interval interval2 interval2))
(width (mul-interval interval1 interval2))
#+end_src

#+RESULTS:
: Adding intervals of same width (5.0)5.0
: 5.0
: 5.0
: Multiplying intervals of same width (5.0)37.5
: 12.5
: 25.0

** 2.10

#+begin_src racket :results output
(define (make-interval a b)
  (cons a b))

(define (lower-bound interval)
  (car interval))

(define (upper-bound interval)
  (cdr interval))

(define (mul-interval x y)
  (let ((p1 (* (lower-bound x) (lower-bound y)))
        (p2 (* (lower-bound x) (upper-bound y)))
        (p3 (* (upper-bound x) (lower-bound y)))
        (p4 (* (upper-bound x) (upper-bound y))))
    (make-interval (min p1 p2 p3 p4)
                   (max p1 p2 p3 p4))))

(define (spans-zero? interval)
  (and (< (lower-bound interval) 0)
       (> (upper-bound interval) 0)))

(define (div-interval x y)
  (if (or (spans-zero? x)
          (spans-zero? y))
      (error "Error, one of the intervals spans zero")
      (mul-interval x
                    (make-interval (/ 1.0 (upper-bound y))
                                   (/ 1.0 (lower-bound y))))))

(define interval1 (make-interval 5.0 10.0))
(define interval2 (make-interval -1.0 5.0))
(div-interval interval1 interval2)
#+end_src

#+RESULTS:
: Error, one of the intervals spans zero
:   context...:
:    body of "/tmp/babel-ByTkdp/org-babel-2Pd3y0.rkt"

** 2.11

This one was quite tedious with all 9 branches to program
individually.

#+begin_src racket :results output
(define (n-positive x)
  (apply + (map
            (lambda (y)
              (if (> y 0)
                  1
                  0))
            x)))

(define (make-interval a b)
  (cons a b))

(define (lower-bound interval)
  (car interval))

(define (upper-bound interval)
  (cdr interval))

(define (mul-interval x y)
  (let ((p1 (* (lower-bound x) (lower-bound y)))
        (p2 (* (lower-bound x) (upper-bound y)))
        (p3 (* (upper-bound x) (lower-bound y)))
        (p4 (* (upper-bound x) (upper-bound y))))
    (make-interval (min p1 p2 p3 p4)
                   (max p1 p2 p3 p4))))

(define   (mul-interval2 x y)
  (let ((x1 (lower-bound x))
        (x2 (upper-bound x))
        (y1 (lower-bound y))
        (y2 (upper-bound y)))
    (cond ((= 4 (n-positive (list x1 x2 y1 y2))) (make-interval (* x1 y1) (* x2 y2)))    ; ++,++
          ((= 0 (n-positive (list x1 x2 y1 y2))) (make-interval (* x2 y2) (* x1 y1)))    ; --,--
          ((and (= 0 (n-positive (list x1)))
                (= 3 (n-positive (list x2 y1 y2)))) (make-interval (* x1 y2) (* x2 y2))) ; -+,++
          ((and (= 0 (n-positive (list y1)))
                (= 3 (n-positive (list x1 x2 y2)))) (make-interval (* y1 x2) (* y2 x2))) ; ++,-+
          ((and (= 0 (n-positive (list x1 x2)))
                (= 2 (n-positive (list y1 y2)))) (make-interval (* x1 y2) (* x2 y1))) ; --,++
          ((and (= 0 (n-positive (list y1 y2)))
                (= 2 (n-positive (list x1 x2)))) (make-interval (* y1 x2) (* y2 x1))) ; ++,--
          ((and (= 1 (n-positive (list x1 x2)))
                (= 0 (n-positive (list y1 y2)))) (make-interval (* x2 y1) (* x1 y1))) ; -+,--
          ((and (= 1 (n-positive (list y1 y2)))
                (= 0 (n-positive (list x1 x2)))) (make-interval (* y2 x1) (* y1 x1))) ; --,-+
          ((and (= 1 (n-positive (list x1 x2)))
                (= 1 (n-positive (list y1 y2))))
           ;; -+,-+
           (if (< x1 y1)
               (make-interval (* x1 y2) (* x2 y2))
               (make-interval (* y1 x2) (* x2 y2))))
          )))

(define   (make-condition name x y)
  (list name x y))

(define   (condition-name condition)
  (car condition))

(define   (condition-intervals condition)
  (cdr condition))

(define   (test-multiply condition)
  (newline) (display (condition-name condition))
  (newline) (display "mul-interval: ") (display
                                        (apply mul-interval (condition-intervals condition)))
  (newline) (display "mul-interval2: ") (display
                                         (apply mul-interval2 (condition-intervals condition))))

(define   conditions
  (list
   (make-condition "++ , ++"
                   (make-interval 1 2)
                   (make-interval 11 12))
   (make-condition "-- , --"
                   (make-interval -1 -2)
                   (make-interval -11 -12))
   (make-condition "-+ , ++"
                   (make-interval -1 2)
                   (make-interval 11 12))
   (make-condition "++ , -+"
                   (make-interval 1 2)
                   (make-interval -11 12))
   (make-condition "-- , ++"
                   (make-interval -2 -1)
                   (make-interval 11 12))
   (make-condition "++ , --"
                   (make-interval 1 2)
                   (make-interval -12 -11))
   (make-condition "-+ , --"
                   (make-interval -1 2)
                   (make-interval -12 -11))
   (make-condition "-- , -+"
                   (make-interval -2 -1)
                   (make-interval -12 11))
   (make-condition "-+ , -+"
                   (make-interval -1 2)
                   (make-interval -12 11))))

(for-each test-multiply conditions)
#+end_src

#+RESULTS:
#+begin_example

++ , ++
mul-interval: (11 . 24)
mul-interval2: (11 . 24)
-- , --
mul-interval: (11 . 24)
mul-interval2: (24 . 11)
-+ , ++
mul-interval: (-12 . 24)
mul-interval2: (-12 . 24)
++ , -+
mul-interval: (-22 . 24)
mul-interval2: (-22 . 24)
-- , ++
mul-interval: (-24 . -11)
mul-interval2: (-24 . -11)
++ , --
mul-interval: (-24 . -11)
mul-interval2: (-24 . -11)
-+ , --
mul-interval: (-24 . 12)
mul-interval2: (-24 . 12)
-- , -+
mul-interval: (-22 . 24)
mul-interval2: (-22 . 24)
-+ , -+
mul-interval: (-24 . 22)
mul-interval2: (-24 . 22)
#+end_example

** 2.12

Set the language when tangling, but don't execute. Using
~jupyter-racket~ uses the ~#lang iracket~ language.

#+begin_src jupyter-racket :results none :tangle ex_2_12.rkt :eval no
#lang sicp
#+end_src

First bring in the previously defined abstractions.

#+begin_src jupyter-racket :results none :session 2-12 :tangle ex_2_12.rkt
(define (make-center-width c w)
  (make-interval (- c w) (+ c w)))

(define (center i)
  (/ (+ (lower-bound i) (upper-bound i)) 2))

(define (width i)
  (/ (- (upper-bound i) (lower-bound i)) 2))

(define (make-interval a b)
  (cons a b))

(define (lower-bound interval)
  (car interval))

(define (upper-bound interval)
  (cdr interval))
#+end_src

Now define ~make-center-percent~.

#+begin_src jupyter-racket :results none :session 2-12 :tangle ex_2_12.rkt
(define (make-center-percent ctr pct)
  (let ((width (* ctr (/ pct 100.0))))
    (make-interval (- ctr width) (+ ctr width))))
#+end_src

Let's check that it works. A center of 10 with a tolerance of 10% should give
an interval of [13.5, 16.5].

#+begin_src jupyter-racket :results value :display plain :session 2-12 :tangle ex_2_12.rkt
(make-center-percent 15 10)
#+end_src

#+RESULTS:
: '(13.5 . 16.5)

Finally add a percent selector for an interval object.

#+begin_src jupyter-racket :results value :display plain :session 2-12 :tangle ex_2_12.rkt
(define (percent i)
  (* 100 (/ (width i) (center i))))

(percent (make-center-percent 15 10))
#+end_src

#+RESULTS:
: 10.0

** 2.13

Let $c_i$, $l_i$, $w_i$, $p_i$ be the center, lower bound, width, and
percent tolerance of an interval $i$.

Let $i_3 = i_1 \times i_2$. From our definition of taking the products
of intervals,

#+NAME: eq:l3
\begin{align}
\begin{split}
  l_3 &= l_1 \times l_2 \\
      &= (c_1 - w_1)(c_2 - w_2) \\
      &= c_1 c_2 - c_1 w_2 - c_2 w_1 + w_1 w_2
\end{split}
\end{align}

From Equation [[eq:l3]], the width of the new interval can be rewritten as

#+NAME: eq:w3
\begin{align}
\begin{split}
  w_3 &= c_3 - l_3 \\
      &= c_1 c_2 - l_3 \\
      &= c_1 w_2 + c_2 w_1 - w_1 w_2
\end{split}
\end{align}

From Equation [[eq:w3]] we can rewrite the definition of $p_3$ as

\begin{align*}
  p_3 &= 100 \frac{w_3}{c_3} \\
      &= 100 \frac{c_1 w_2 + c_2 w_1 - w_1 w_2}{c_1 c_2} \\
      &= 100 \frac{w_2}{c_2} + 100 \frac{w_1}{c_1} - 100 \frac{w_1 w_2}{c_1 c_2} \\
      &= p_1 + p_2 - \frac{p_1 p_2}{100} \\
      &\approx p_1 + p_2
\end{align*}

So for small tolerances, the percentage tolerance of the product is
approximately the sum of the tolerances.

** 2.14

First set up the code from before to test.

#+begin_src jupyter-racket :session 2-14 :results none
(define (make-center-width c w)
  (make-interval (- c w) (+ c w)))

(define (center i)
  (/ (+ (lower-bound i) (upper-bound i)) 2))

(define (width i)
  (/ (- (upper-bound i) (lower-bound i)) 2))

(define (make-interval a b)
  (cons a b))

(define (lower-bound interval)
  (car interval))

(define (upper-bound interval)
  (cdr interval))

(define (make-center-percent ctr pct)
  (let ((width (* ctr (/ pct 100.0))))
    (make-interval (- ctr width) (+ ctr width))))

(define (percent i)
  (* 100 (/ (width i) (center i))))

(define (mul-interval x y)
  (let ((p1 (* (lower-bound x) (lower-bound y)))
        (p2 (* (lower-bound x) (upper-bound y)))
        (p3 (* (upper-bound x) (lower-bound y)))
        (p4 (* (upper-bound x) (upper-bound y))))
    (make-interval (min p1 p2 p3 p4)
                   (max p1 p2 p3 p4))))

(define (div-interval x y)
  (if (or (spans-zero? x)
          (spans-zero? y))
      (error "Error, one of the intervals spans zero")
      (mul-interval x
                    (make-interval (/ 1.0 (upper-bound y))
                                   (/ 1.0 (lower-bound y))))))

(define (spans-zero? interval)
  (and (< (lower-bound interval) 0)
       (> (upper-bound interval) 0)))
#+end_src

#+begin_src jupyter-racket :session 2-14 :results output
(define A (make-center-percent 100 1))
(define B (make-center-percent 50 1))

(define AdivB (div-interval A B))
(define BdivA (div-interval B A))
(define AdivA (div-interval A A))
(define BdivB (div-interval B B))

(print AdivB) (newline)
(print BdivA) (newline)
(print AdivA) (newline)
(print BdivB) (newline)
#+end_src

#+RESULTS:
: '(1.9603960396039604 . 2.0404040404040407)
: '(0.4900990099009901 . 0.5101010101010102)
: '(0.9801980198019802 . 1.0202020202020203)
: '(0.9801980198019802 . 1.0202020202020203)

Let's check the centers of each interval.

#+begin_src jupyter-racket :session 2-14 :results output
(print (center AdivB)) (newline)
(print (center BdivA)) (newline)
(print (center AdivA)) (newline)
(print (center BdivB)) (newline)
#+end_src

#+RESULTS:
: 2.0004000400040005
: 0.5001000100010001
: 1.0002000200020003
: 1.0002000200020003

These are all off at the millionths place.

** 2.15

Looking back at the previous problem, all the centers shifted away
from zero. This provides a clue about what is going on. There is
inherent asymmetry in our intervals where the side towards zero is
bounded by zero while the side away from zero is unbounded to
infinity. This causes the center of the interval to shift
slightly. Correcting this would require having uneven intervals on
either side of the "center" point.

Then there is the issue of repeated intervals that represent the same
measurement / interval. It is not meaningful to multiply and divide
these intervals in the same way as two independent intervals because
their uncertainty is 100% dependent. There is no uncertainty/error to
propagate when doing ~A / A~, but there is for ~A / B~. Because of
this, Eva is right and ~par2~ is the better program.

I actually ran into a very similar problem in an undergraduate course
on applied quantum mechanics where I had experimental data that I was
using to measure the mass of an electron (I forget exactly which
experiment we conducted). There were two mathematical formulas that I
could use to go from the data to the mass, and one involved either
more parameters with uncertainty or the same parameter multiple times
(I can't remember). This resulted in two estimates for the electron
mass with different uncertainty intervals.

** 2.16

The different algebraic expressions are giving different answers
because they are not tracking the identity of each interval. The
current package treats all the intervals like they are different and
does no simplification to assert that the uncertainties should not
compound in multiplication and division operations.

I am not sure if this problem is solvable because I don't have a proof
that shows that all problems can be rewritten to use each interval
only once in the expression.

** 2.17

#+begin_src jupyter-racket :session 2-17 :results none
(define (last-pair x)
  (if (null? (cdr x))
      (car x)
      (last-pair (cdr x))))
#+end_src

Use the example in the book as a test, ~(last-pair (list 23 72 149 34)) => (34)~.

#+begin_src jupyter-racket :session 2-17 :results value :display plain
(last-pair (list 23 72 149 34))
#+end_src

#+RESULTS:
: 34

** 2.18

As mentioned many times in the lectures, we can use wishful thinking
to solve this problem. If we have a list ~(x y z)~, we can break it
into the first value ~car~ and the rest of the list ~cdr~. To reverse
the list, we just want to append ~car~ to ~reverse cdr~.

#+begin_src jupyter-racket :session 2-18 :results none
(define (append list1 list2)
  (if (null? list1)
      list2
      (cons (car list1) (append (cdr list1) list2))))

(define (reverse x)
  (if (null? x)
      '()
      (append (reverse (cdr x)) (list (car x)))))
#+end_src

Test with the given example: ~(reverse (list 1 4 9 16 25)) => (25 16 9 4 1)~.

#+begin_src jupyter-racket :session 2-18 :results output
(print (reverse (list 1 4 9 16 25)))
#+end_src

#+RESULTS:
: '(25 16 9 4 1)

** 2.19

Start with the code provided by the problem.

#+begin_src jupyter-racket :session 2-19 :results none
(define us-coins (list 50 25 10 5 1))
(define uk-coins (list 100 50 20 10 5 2 1 0.5))

(define (cc amount coin-values)
  (cond ((= amount 0) 1)
        ((or (< amount 0) (no-more? coin-values)) 0)
        (else
         (+ (cc amount
                (except-first-denomination
                 coin-values))
            (cc (- amount
                   (first-denomination
                    coin-values))
                coin-values)))))
#+end_src

We need to implement ~first-denomination~,
~except-first-denomination~, and ~no-more?~.

#+begin_src jupyter-racket :session 2-19 :results none
(define (first-denomination coin-values)
  (car coin-values))

(define (except-first-denomination coin-values)
  (cdr coin-values))

(define (no-more? coin-values)
  (null? coin-values))
#+end_src

Test code from the book.

#+begin_src jupyter-racket :session 2-19 :results output
(define us-coins (list 50 25 10 5 1))
(define us-coins-reverse (list 1 5 10 25 50))
(define uk-coins (list 100 50 20 10 5 2 1 0.5))

(print (cc 100 us-coins)) (newline)
(print (cc 100 us-coins-reverse))
#+end_src

#+RESULTS:
: 292
: 292

There is no difference if the order is changed because it counts every
combination of change.

** 2.20

Scheme didn't have ~filter~ during SICP, so I implement my own version
here.

#+begin_src jupyter-racket :session 2-20 :results none
(define (filter p? x)
  (cond ((null? x) '())
        ((p? (car x)) (cons (car x) (filter p? (cdr x))))
        (else (filter p? (cdr x)))))

(define (same-parity x . y)
  (if (odd? x)
      (filter odd? (append (list x) y))
      (filter even? (append (list x) y))))
#+end_src

#+begin_src jupyter-racket :session 2-20 :results output
(display (same-parity 1 2 3 4 5 6 7)) (newline)
(display (same-parity 2 3 4 5 6 7))
#+end_src

#+RESULTS:
: (1 3 5 7)
: (2 4 6)

** 2.21

#+begin_src jupyter-racket :session 2-21 :results output
(define (square x) (* x x))

(define (square-list items)
  (if (null? items)
      '()
      (cons (square (car items)) (square-list (cdr items)))))

(display (square-list (list 1 2 3 4)))
#+end_src

#+RESULTS:
: (1 4 9 16)

#+begin_src jupyter-racket :session 2-21 :results output
(define (square-list2 items)
  (map square items))

(display (square-list (list 1 2 3 4)))
#+end_src

#+RESULTS:
: (1 4 9 16)

** 2.22

First let's look at the behavior.

#+begin_src jupyter-racket :session 2-22 :results output
(define (square x) (* x x))

(define (square-list items)
  (define (iter things answer)
    (if (null? things)
        answer
        (iter (cdr things)
              (cons (square (car things))
                    answer))))
  (iter items '()))

(display (square-list (list 1 2 3 4)))
#+end_src

#+RESULTS:
: (16 9 4 1)

As described in the book, the results are in reverse order. This
happens because it keeps cons-ign the current value to the beginning
of the answer list. Instead, it needs to be appending to the end of
the list.

The attempted fix doesn't work.

#+begin_src jupyter-racket :session 2-22 :results output
(define (square-list items)
  (define (iter things answer)
    (if (null? things)
        answer
        (iter (cdr things)
              (cons answer
                    (square (car things))))))
  (iter items '()))

(display (square-list (list 1 2 3 4)))
#+end_src

#+RESULTS:
: ((((() . 1) . 4) . 9) . 16)

This successfully gets the elements in the right order, but they are
in a nested list. This demonstrates an incorrect implementation of
append where you cons a list with a value.

** 2.23

I first tried to do this with an ~if~, but I couldn't have multiple
expressions in either of the branches, so I switched to a ~cond~.

#+begin_src jupyter-racket :session 2-23 :results output
(define (for-each f xs)
  (cond ((not (null? xs))
        (f (car xs))
        (for-each f (cdr xs)))
        (else '())))

(for-each (lambda (x)
            (newline)
            (display x))
          (list 57 321 88))
#+end_src

#+RESULTS:
: 
: 57
: 321
: 88

** 2.24

The result from the interpreter is going to be ~(1 (2 (3 4)))~. Let's
check.

#+begin_src jupyter-racket :session 2-24 :results output
(display (list 1 (list 2 (list 3 4))))
#+end_src

#+RESULTS:
: (1 (2 (3 4)))

We can get a nice box-and-pointer diagram using the ~sdraw~ package in
racket.

#+begin_src sh :results none
raco pkg install sdraw
#+end_src

#+begin_src jupyter-racket :session 2-24 :display html
(require sdraw)

(sdraw (list 1 (list 2 (list 3 4))))
#+end_src

#+RESULTS:
#+begin_export html
<code><img style="display: inline; vertical-align: baseline; padding: 0pt; margin: 0pt; border: 0pt" src="data:image/png;base64,iVBORw0KGgoAAAANSUhEUgAAAbAAAACUCAYAAADyBHy+AAARmUlEQVR4nO3df2zc9X3H8df7
YnwhJZ3UBNelS1rMOT+a8sdIgjthp2F/dNgTUpGGVrvjR/mD8UcrhEZDlBRBGp/DhukqSpi0
FiVbMhV1laqpKqHTtEwLf5Qk6qbyB4Rgs9kapfLJVaBL+F6Se++PuzPn89l3Z5/vc1/8fEgf
yf58v/fh7bdynzfv7339tUl6QNJ3Ja1VWO9LetjdjwSOY0FmRr4AoAWYpIykdaEDKci4+/Wh
g1iImZEvAGgBCRU245GREUVRJHeveURRpHQ6PbNYA9ZY3/wU1I18AUALMEkuSVEUqb29ve4F
oijS6tWrZ75e6hrubnUv0ERmRr4AoAUkil8sZiOVpGQyOfN1I9aIC/IFAI2X7kvdle7t/nHx
++Fd3Y+kd216utK5iUqTpTKZjE6fPq1MJrPogBqxRrOY2ZiZHTazzsW8fqXlCwDmU+9+eqhn
6zqZfd8sf2VQkhKuC3J/dLgvdUf5+fMWsOnpaQ0NDamjo0M9PT3q6OjQ0NCQpqenaw6+EWsE
8LKkhySNm9nztSZ+BecLAOZT1356NXn5YbnW5nLaW5y7/MpbRyX7lckOVnqNS/Jy/f39XjxW
OgYGBuac2+A16rqpodFDUqeki4V4IkkfSDoq6YbCcfLFYDAYNYxq+2n5GO7r/tlwb+rVOfO3
pQ4M96ayT+7e3Va2/tzNdGJiouJGWhwTExOzzl/MGjEbpYknXwwGg7H4MW8hG+5NnTvYm/rH
OQWsN3XPcG/KD/Vu6iqdr3gJcXJystJ0zcdrPSdG2iUlJd1T6SD5AoCale6nZ8qOXTLXx8pf
4G5rJClqy14snW+rtPqGDRsW/K9XO17LORMTE7POMbNCoOFvCzezw8o/oWR1YSqr/P85vCjp
vvLzV3q+AGA+VfbTfWWnvynTzXPWSPgWud574uTb75bOV+zANmzYoP7+/orBDAwM1LwhL3WN
EAofMn5N+WRnlW93fyipy93vr/SalZwvAJhPtf3U3d+Z/Qr/V0lbDvZt6inOfG+gOynXV0z6
t/L1570L8fjx4xocHCwNRIODgzp27FjNwRfXKHYLi1kjgMeVb28vSXpB0mcrJ3q2FZwvAJhP
XfvpmqvRMUlTCemx4tyF3/m9kjpduWfKz595Ekfxjo45JxQ206mpKa1fX/nJRSWXsyoez2Qy
Gh8fV1dXVy1rBL0kZmZjyt/6edDd361wnHwBQA2q7aeVpPu675Z07/5T5++UpPSuTXvMfeO+
U+e/Pmd91VjA5jte6znVxGVDrrWAkS8AWF4zlxCz2eyiFoiiaObrRqwRF+QLAMKaKWCjo6N1
b6jZbFajo6Mz3zdijbggXwAQlkmaUuv8WY4pd+8IHcRCzIx8AUALSEjaI+m90IEoH8Oe0EHU
gHwBQAuwajcSlNy0wM0CNSBfANAcVf+cCgAArYgCBgCIJQoYACCWKGAAgFiigAEAYokCBgCI
JQoYACCWKGAAgFiigAEAYokCBgCIJQoYACCWKGAAgFiigAEAYokCBgCIJQoYACCWKGAAgFiq
WMDMbMzMDptZZ9l8Z2F+rDnhxQP5AoDmm68De1nSQ5LGixNm9pykscL8z5c/tFghXwDQZObu
cyfzncS4pGtLpiNJSUmXJHW5+7tNiTAGyBcANF/FDqyw2R6R9EHJdFL5TfkIm/Fs5AsAmq9i
BybN21VckpRy93eaEFuskC8AaK5570Ks0FUUuwk24wrIFwA017wdmDSnq6CbqIJ8AUDzLPh7
YCVdRU7SUTbjhZEvAGieBTswaaarOCNpJzcjSGb2gKTvSlobOJT3JT3s7kcCx7Eg8gVguVQt
YJjNzDKS1oWOoyDj7teHDmIh5AvAcuFRUvVbJ0kjIyOKokjuXvOIokjpdHpmoQassT7Az18v
8gVgWSzYge0Y+dwtSuT+RNIfmezAmb2v/3vTImtRZuaSFEWR2tvb6359FEVavXr1zNdLXcPd
re4Fmoh8AVgu83Zg3d/rTiqR+6mk+yXtzslXNy2qGFjMRipJyWSyoWvEBfkCGiPdl7or3dv9
4+L3w7u6H0nv2vR0yJhCmbeAnf/G+ejs3jc+3RZd/YNmBhR3mUxGp0+fViaTCbpGs8z3IONa
rbR8AaXqff8c6tm6TmbfN9PMpbOE64LcHx3uS92xfJG2Jj4Da5Dp6WkNDQ2po6NDPT096ujo
0NDQkKanp5u6RgAzDzI2s+drfSOu4HwBpep6/1xNXn5YrrW5nPYW5y6/8tZRyX5lsoPLHGvr
qfYheM+TqY9vP7TZbzm0+Y56Pjz/qA5Jnk/bbP39/V48VjoGBgbmnNvgNULno1PSxUI8kfJP
Ijkq6QbyxWAsPKq9f8rHcF/3z4Z7U6/Omb8tdWC4N5V9cvfuttA/UzNHW2PL4co0OTmpEydO
VDz20ksvaXJyUhs2bFjSGmaV7z0o3iTRIoofUg1K+oqZvVjpJPIFVFTp/bPPSx+I4J5y6eyc
V5reknRN8so7G1XyZ50+6ha8hLjzrzb3X0km/jz/nX1p+1Ob/+z2AzdyM0eZycnJJR2v9ZwY
aVf+afz3VDpIvoAFlb5/zpQdu2Suj5W/wN3WSFLUlr24/OG1jnk7sG0HtrVfm7S/l3S9JJn8
Eck+eP+aa2+T9MtmBRgH1bqFasdrOWdiYmLWOcUOw1vgtnAzOyzpAUnF/7nJKn9J5EVJ95Wf
v9LzBZSq8v7ZV3b6mzLdPGeNhG+R670nTr69sp6WFPoaZtyG+EynPB98BsZgLHJUe/+Uj+He
mx4c7k35t3u7e4pzz/anksO9qV+ne1M/Cf3zNHtwF2KDHD9+XIODgzPfm5kGBwd17Nixutco
dguLWSOAx/XhX55+QdJn3f1+r/Ig4xWcL6BUXe+fNVejY5KmEtJjxbkLv/N7JXW6cs80I+BW
wrMQ61S8CWC+vBU306mpKa1fX/nJRSWXsyoez2QyGh8fV1dXVy1rBL0kZmZjyt8KfNArPOyZ
fAHzq/b+qSTd1323pHv3nzp/pySld23aY+4b9506//VlDLUlUcDqVOuGvFBeazmnhjiKa7T0
hky+ACwXLiEuUjabXdTroihq6BpxQb4ANBoFbJFGR0fr3lCz2axGR0cbukZckC8AjcYlxDqZ
2ZRa589yTLl7R+ggFkK+ACwXOrD67ZH0XugglI9hT+ggakC+ACwLOrAGK7lpgZsFakC+ACwW
HRgAIJYoYACAWKKAAQBiiQIGAIglChgAIJYoYACAWKKAAQBiiQIGAIglChgAIJYoYACAWKKA
AQBiiQIGAIglChgAIJYoYACAWKKAAQBiiQIGAIglClgDmNmYmR02s86y+c7C/Fio2FoR+QLQ
CBSwxnhZ0kOSxosTZvacpLHC/M8DxdWqyBeAJTN3Dx1D7BU6iXFJ15ZMR5KSki5J6nL3d0PE
1orIF4BGoANrgMJme0TSByXTSeU35SNsxrORLwCNQAfWIPN0FZckpdz9nTBRtS7yBWCp6MAa
pEJXUewm2IwrIF8AlooOrIHKugq6iSrIF4CloANroJKuIifpKJvxwsgXgKWgA2uwQldxRtJO
bkaQzOwBSd+VtDZwKO9LetjdjwSOY0HkC6gdBQzLyswyktaFjqMg4+7Xhw5iIeQLqB2XELHc
1knSyMiIoiiSu9c8oihSOp2eWagBa6wP8PPXi3wBNaIDa6CeQ1s3XTF/2lzb3PxEImHPndnz
xrnQcYVkZi5JURSpvb297tdHUaTVq1fPfL3UNdzd6l6gicgXUDsKWINsO7DtumuTV8ckvSvp
hGR3Sp5YG33q5pNPnLwSOr5QihvyUv6dmeX30Aat0dIbMvlaunRf6i65fXX/K+f/VJKGd3U/
YrIb9v/Hm98MHRsai0uIDZL9RPayyQ7qE9ftOLv3jb3m/h1JW37b9s4nQ8fWyjKZjE6fPq1M
JhN0jWaZ70HGtSJfCzvUs3WdzL5vppnqnXBdkPujw32pO5YvUoRAAWuQ8984H53Z+/pzZx88
e/mWQ1v/0E1/I+lf/mv/uf8NHVsrmp6e1tDQkDo6OtTT06OOjg4NDQ1penq6qWsEMPMgYzN7
vtaNmXzVlq+rycsPy7U2l9Pe4tzlV946KtmvTHZwmWNFs9XzAS+j+tg5suWL2w9tvrD90ObX
bx3Zsi50PKGHJM//M5utv7/fi8dKx8DAwJxzG7xG6Hx0SrpYiCdS/kkkRyXdQL7qz1f5GO7r
/tlwb+rVOfO3pQ4M96ayT+7e3Rb6Z2I0bvAZWAPtOLT5yzL7oaQ323LXfOkX+177TeiYQqv0
mc7k5KQ2btw472smJia0YcOG0jVU7xoxk1V+g35R0n0S+aqiNF/7vOQX4NN93edcOvutU+e/
WvqCdF/3PZL+YZUSN+09dW5c+EjgEmKD7Hhq65dk9mNJbSY/ejlxpX/nU1v2bzuwrf7bwD7i
Jicnl3S81nNipF35p/HfU+kg+ZqjNF9nyo5dMtfHyl/gbmskKWrLXlz+8NAsFLAGMWmTpFWS
2lz2HZMfcWl4zTW+KXRsraa0W1jM8VrOmZiYmHWpocjdLfSQ9Lxm/ymZrPKXx46Rr7rztbPs
R3lTps3lP58lfIuk9544+faKfzrOR0roa5iMj/YQn+mU54PPwBqYr/Ix3HvTg8O9Kf92b3dP
ce7Z/lRyuDf163Rv6iehfx5GYwcdGII4fvy4BgcHZ743Mw0ODurYsYpNyIJrFD/zWcwaATyu
D//y9AuSPuvu93uVBxmTr9ryteZqdEzSVEJ6rDh34Xd+r6ROV+6ZZgSM5uEmDiyrar+YW9xM
p6amtH595ScXVfvF3Ewmo/HxcXV1ddWyRtBfzDWzMeVvDT/oFR72TL7mxLFgvipJ93XfLene
/afO3ylJ6V2b9pj7xn2nzn99GUNFABQwLKtaN+SF/h2upCdLkC+gdlxCRFNks9lFvS6Kooau
ERfkC6iOAoamGB0drXtDzWazGh0dbegacUG+gOq4hIhlZWZTap0/yzHl7h2hg1gI+QJqRweG
5bZH0nuhg1A+hj2hg6gB+QJqRAeGoEpuWuBmgRqQL+BDdGAAgFiigAEAYokCBgCIJQoYACCW
KGAAgFiigAEAYokCBgCIJQoYACCWKGAAgFiigAEAYokCBgCIJQoYACCWKGAAgFiigAEAYokC
BgCIJQoYACCWKGBoOjMbM7PDZtZZNt9ZmB8LFVsrIl9AZRQwhPCypIckjRcnzOw5SWOF+Z8H
iqtVkS+gAnP30DFghSl0EuOSri2ZjiQlJV2S1OXu74aIrRWRL6AyOjA0XWGzPSLpg5LppPKb
8hE249nIF1AZHRiCmKeruCQp5e7vhImqdZEvYC46MARRoasodhNsxhWQL2AuOjAEU9ZV0E1U
Qb6A2ejAEExJV5GTdJTNeGHkC5iNDgxBFbqKM5J2cjNCdeQL+BAFDAAQS1xCBADEUlvoALBy
bX9q63aTPyTpiya9avJnT+89dyZ0XK1ux6Gt90k+aAn97ZnH3vjn0PEAodCBIZiE9B3Jbnbp
Ry7dmJP95PYDN64OHVcru/XpbZ0yf1amP/acPhM6HiAkOjAEc2bv61+UpFvSn/uMteWSct32
/nXt12n2EydQInfl6jMyTYeOA2gFdGAIasdTW+5OrMr9t1yPSvqns395LhM6pla1Y2TL7TIN
meubkl8KHQ8QGgUMQV2KVp1w2dcke1XSnduf2pYKHVMr2vF3O65RQoclf8s9MS5Zu0y/Fzou
ICRuo0dL+MKB7o9fSa76reR/cXbvuR+EjqfV7Bz53Oc9kXutfD6X0Kd/uecNfqEZKxKfgSGI
L4zc/MnLiey3TImfmnK5XLJtwOQJU+Kt0LG1Ir+8ZtyS/3d7Tp4w9y/L7EGZTvznnnO/Dh0b
EAodGIK45a+33JDI6SVJn5e0SlLWZYdvuumNb/7obr8aOLyWdevT2zpzV6++KWmtpPfbctd0
/2Lfa78JHRcQAgUMQe04sGON1lz41NqLv/8/J584eSV0PADigwIGAIgl7kIEAMQSBQwAEEsU
MABALP0/+6LFIrVHDEcAAAAASUVORK5CYII=
"/></code>
#+end_export

** 2.25

#+begin_src jupyter-racket :session 2-25 :results value :display plain
(define x (list 1 3 (list 5 7) 9))

(car (cdr (car (cdr (cdr x)))))
#+end_src

#+RESULTS:
: 7

#+begin_src jupyter-racket :session 2-25 :results value :display plain
(define y (list (list 7)))

(car (car y))
#+end_src

#+RESULTS:
: 7

We need to ~(car (cdr (x))~ all the way down the nested list.

#+begin_src jupyter-racket :session 2-25 :results value :display plain
(define z '(1 (2 (3 (4 (5 (6 7)))))))

(car (cdr
      (car (cdr
            (car (cdr
                  (car (cdr
                        (car (cdr
                              (car (cdr z))))))))))))
#+end_src

#+RESULTS:
: 7

** 2.26

~(append x y)~ will result in ~'(1 2 3 4 5 6)~

#+begin_src jupyter-racket :session 2-26 :results value :display plain
(define x (list 1 2 3))
(define y (list 4 5 6))

(append x y)
#+end_src

#+RESULTS:
: '(1 2 3 4 5 6)

~(cons x y)~ will result in ~(list (list 1 2 3) 4 5 6)~

#+begin_src jupyter-racket :session 2-26 :results value :display plain
(cons x y)
#+end_src

#+RESULTS:
: '((1 2 3) 4 5 6)

~list x y~ will result in ~(list (list 1 2 3) (list 4 5 6))~

#+begin_src jupyter-racket :session 2-26 :results value :display plain
(list x y)
#+end_src

#+RESULTS:
: '((1 2 3) (4 5 6))

** 2.27

All we need to do is add one more case to the case analysis and
reverse the ~car~ in addition to the ~cdr~. If the input is not a
pair, return the input because there is nothing to reverse.

#+begin_src jupyter-racket :session 2-27 :results output
(define (reverse x)
  (if (null? x)
      '()
      (append (reverse (cdr x))
              (list (car x)))))

(define (deep-reverse x)
  (cond ((null? x) '())
        ((not (pair? x)) x)
        (else (append (deep-reverse (cdr x))
                      (list (deep-reverse (car x)))))))

(define x (list (list 1 2) (list 3 4)))

(display x)
(newline) (display (reverse x))
(newline) (display (deep-reverse x))

(newline)
(define y '(1 (2 3) (4 (5 6))))
(newline) (display y)
(newline) (display (reverse y))
(newline) (display (deep-reverse y))
#+end_src

#+RESULTS:
: ((1 2) (3 4))
: ((3 4) (1 2))
: ((4 3) (2 1))
: 
: (1 (2 3) (4 (5 6)))
: ((4 (5 6)) (2 3) 1)
: (((6 5) 4) (3 2) 1)


# local variables:
# org-display-inline-images: t
# org-confirm-babel-evaluate: nil
# latexpreview: t
# end:
